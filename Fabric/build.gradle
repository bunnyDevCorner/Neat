plugins {
    id 'fabric-loom' version '1.7.2'
}

version = "${mc_version}-${build_number}-FABRIC"

loom {
    accessWidenerPath = file("src/main/resources/neat.accesswidener")
    mixin.defaultRefmapName = "neat.refmap.json"

    runs {
        configureEach {
            runDir "../run"
            ideConfigGenerated(true)
        }
    }

}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:0.18.4"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.116.4+1.21.1"

    compileOnly project(":Xplat")

    modApi("me.shedaniel.cloth:cloth-config-fabric:15.0.140") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation("com.terraformersmc:modmenu:11.0.3")
}

compileJava {
    source(project(":Xplat").sourceSets.main.allSource)
    options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"

}

sourcesJar {
    from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
